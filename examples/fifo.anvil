
type fifo_req = [
    | Push of logic
    | Pop
]

type fifo_resp = [
    | Succ of logic
    | Fail
]

chan fifo_ifc = {
    left req : (fifo_req@#0-#1) @#1 - @#1,
    right resp : (fifo_resp@#0-#1) @req - @req
}

proc fifo_impl (i: left fifo_ifc) =
    reg fifo : logic[8]
    reg cnt : logic[4]
    cycle then
    recv req = i::req then
    let resp =
        match req with
        |> Push data ->
            if !cnt == 4'd8 then
                fifo_resp::Fail
            else (
                set cnt := (!cnt) + 4'b1;
                set fifo := (!fifo) | ({7'b0, data} << !cnt);
                fifo_resp::Succ data
            )
        |> Pop ->
            if !cnt == 4'd0 then
                fifo_resp::Fail
            else (
                set cnt := (!cnt) - 4'b1;
                set fifo := (!fifo) >> 1'b1;
                fifo_resp::Succ (!fifo)[0]
            )
        done
    in
    send i::resp (resp) then ()

proc fifo () =
    chan foreign le -- ri : fifo_ifc
    spawn fifo_impl (le)
    reg cnt : logic[4]
    reg filled : logic
    cycle then
    if !filled then
        set cnt := !cnt - 4'b1;
        if !cnt == 4'd1 then dfinish
        else (
            send ri::req (fifo_req::Pop) then
            recv r = ri::resp then
            let _ = match r with
            |> Succ data -> dprint "Success = %d" (data); 1'b0
            |> Fail -> dprint "Failed" (1'b0); 1'b0
            done in ()
        )
    else (
        set cnt := !cnt + 4'b1;
        if !cnt == 4'd7 then
            set filled := 1'b1
        else (
            let v = 8'b10110101 in
            send ri::req (fifo_req::Push (v >> !cnt)[0]) then
            recv r = ri::resp then
            let _ = match r with
            |> Succ data -> dprint "Success = %d" (data); 1'b0
            |> Fail -> dprint "Failed" (1'b0); 1'b0
            done in ()
        )
    )

